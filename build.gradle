
buildscript {

    ext {
        springBootVersion = '2.6.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

allprojects {
    group 'team.comito'
    version '1.0.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '11'
    targetCompatibility = '11'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {
        compileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    test {
        useJUnitPlatform()
    }
}


// 각 프로젝트에서만 사용하는 의존성 추가
project(':trade-common') {

    // core 에서는 bootjar로 패키징 할 필요가 없으므로 추가
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
    }
}

project(':trade-chat') {
    dependencies {
        // 컴파일시 core 모듈을 가져온다.
        compileOnly project(":trade-common")
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}

project(':trade-commerce') {
    dependencies {
        // 컴파일시 core 모듈을 가져온다.
        compileOnly project(":trade-common")
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}

project(':trade-welcome') {
    dependencies {
        // 컴파일시 core 모듈을 가져온다.
        compileOnly project(":trade-common")
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}


